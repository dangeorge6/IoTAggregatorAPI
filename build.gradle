buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE")
    }
}

apply plugin: "idea"
apply plugin: "groovy"
apply plugin: "spring-boot"
apply plugin: "jacoco"

mainClassName = "com.shoppertrak.examplews.Main"
def version = '0.0'
def appName = "Example Web Service"
def jarFilename = "ExampleWS.jar"
def springBootVersion = "1.2.5.RELEASE"

def version2 = System.env.BUILD_NUMBER ? System.env.BUILD_NUMBER : System.env.USERNAME
def builtBy = System.env.BUILD_TAG ? "jenkins" : System.env.USERNAME

sourceCompatibility = 1.7
targetCompatibility = 1.7

idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        csv.enabled false
    }
}

repositories {
    maven { url "http://repo.rctanalytics.com/artifactory/repo" }
    mavenLocal()
    mavenCentral()
}

sourceSets {
    main {
        resources.excludes = ["*.jar"]
    }
    integrationTest {
        java.srcDir file("src/integrationTest/groovy")
        resources.srcDir file("src/integrationTest/resources")
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

// This is required for Jenkins to run the unit tests
test.dependsOn integrationTest

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-security:$springBootVersion"

    compile "org.codehaus.groovy:groovy-all:2.4.4"
    compile "com.zaxxer:HikariCP-java6:2.3.9"

    compile files(
            "src/main/resources/ojdbc7.jar"
    )

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
    testCompile "cglib:cglib-nodep:3.1"
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

    integrationTestCompile sourceSets.main.output
    integrationTestCompile configurations.testCompile
    integrationTestCompile sourceSets.test.output
    integrationTestRuntime configurations.testRuntime
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.5"
}

processResources {
    expand([
            appName           : appName,
            appVendor         : "ShopperTrak RCT, INC.",
            builtBy           : builtBy,
            buildDate         : new Date(),
            implementationDate: new Date(),
            versionString     : version + "." + version2
    ])
}

jar {
    archiveName = jarFilename
}
